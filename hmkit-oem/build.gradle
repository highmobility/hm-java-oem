apply plugin: "java-library"
apply from: "deploy.gradle"

sourceCompatibility = 1.8

dependencies {
    if (project.depLocation == 0) {
        implementation project(':hmkit-crypto')
        implementation project(':hmkit-utils')
        implementation project(':hmkit-core-jni')
    } else {
        api deps.crypto
        api deps.corejni
        api deps.utils
    }

    implementation 'org.slf4j:slf4j-api:1.7.25'

    testImplementation 'org.slf4j:slf4j-simple:1.7.25'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

jar {
    from '../lib/libhmbtcore.jnilib'
}

apply from: "https://raw.githubusercontent.com/tonisives/tools/master/gradle/deploy-java-v2.gradle"
apply from: "https://raw.githubusercontent.com/tonisives/tools/master/gradle/jupiter-test-shell-logging.gradle"

task buildCoreOsx {
    doLast {
        exec {
            workingDir '.'
            commandLine 'sh', '-c', 'cd src/main/jni && make clean && make -f Makefile-osx && cd -'
        }
    }
}

task buildCoreLinux {
    doLast {
        exec {
            workingDir '.'
            commandLine 'sh', '-c', 'cd src/main/jni && make clean && make && cd -'
        }
    }
}
